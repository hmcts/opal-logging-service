import org.gradle.api.plugins.quality.Checkstyle

buildscript {
  dependencies {
    classpath("org.flywaydb:flyway-database-postgresql:11.20.0")
  }
}
plugins {
  id 'application'
  id 'jacoco'
  id 'io.spring.dependency-management' version '1.1.7'
  id 'org.springframework.boot' version '3.5.8'
  id 'com.github.ben-manes.versions' version '0.53.0'
  id 'org.sonarqube' version '7.2.2.6593'
  id 'org.flywaydb.flyway' version '11.20.0'
  /*
    Applies analysis tools including checkstyle and OWASP Dependency checker.
    See https://github.com/hmcts/gradle-java-plugin
 */
  id 'uk.gov.hmcts.java' version '0.12.67'
  id "org.openapi.generator" version "7.18.0"
}

application {
  mainClass = 'uk.gov.hmcts.opal.logging.Application'
  group = 'uk.gov.hmcts.opal'
  version = '0.0.1'
}

java {
  toolchain {
    languageVersion = JavaLanguageVersion.of(21)
  }
}

def configureSourceSet(String name) {
  sourceSets.create(name) { sourceSet ->
    sourceSet.java {
      compileClasspath += sourceSets.main.output + sourceSets.test.output
      runtimeClasspath += sourceSets.main.output + sourceSets.test.output
      srcDir "src/${name}/java"
    }
    sourceSet.resources.srcDir "src/${name}/resources"
  }
}

sourceSets {
  openapi {
    java {
      srcDir file('src/openApi/java')
    }
    resources.srcDir file('src/openApi/resources')
  }
  main {
    java {
      srcDir "$buildDir/generated/openapi/src/main/java"
    }
  }
}

['smokeTest', 'integrationTest', 'functionalTest'].each { configureSourceSet(it) }

configurations {
  openapiImplementation.extendsFrom implementation
  openapiRuntimeOnly.extendsFrom runtimeOnly

  functionalTestImplementation.extendsFrom testImplementation
  functionalTestRuntimeOnly.extendsFrom runtimeOnly

  integrationTestImplementation.extendsFrom testImplementation
  integrationTestRuntimeOnly.extendsFrom runtimeOnly

  smokeTestImplementation.extendsFrom testImplementation
  smokeTestRuntimeOnly.extendsFrom runtimeOnly
}
tasks.register("bundleOpenApi", JavaExec) {
  dependsOn 'compileOpenapiJava'
  classpath = sourceSets.openapi.runtimeClasspath
  group = "openapi"
  description = "Bundle multiple OpenAPI YAML files into one"
  mainClass = 'uk.gov.hmcts.opal.openapi.OpenApiBundler'
  args = ["$rootDir/src/main/resources/openapi", layout.buildDirectory.file("openapi-bundled.yaml").get().asFile.absolutePath]
}

openApiGenerate {
  generatorName = "spring"
  inputSpec = layout.buildDirectory.file("openapi-bundled.yaml").get().asFile.absolutePath
  outputDir = layout.buildDirectory.dir("generated/openapi").get().asFile.absolutePath
  apiPackage = "uk.gov.hmcts.opal.logging.generated.http.api"
  modelPackage = "uk.gov.hmcts.opal.logging.generated.dto"
  generateApiTests = false
  generateModelTests = false
  skipOperationExample = true
  openapiNormalizer = [
          REF_AS_PARENT_IN_ALLOF: "true"
  ]
  configOptions = [
          dateLibrary                         : "java8",
          interfaceOnly                       : "true",
          useTags                             : "false",
          useSpringBoot3                      : "true",
          containerDefaultToNull              : "true",
          generatedConstructorWithRequiredArgs: "false",
          useBeanValidation                   : "true",
          openApiNullable                     : "false"
  ]
}
tasks.withType(JavaCompile).configureEach {
  options.compilerArgs << "-Xlint:unchecked" << "-Werror"
}

// https://github.com/gradle/gradle/issues/16791
tasks.withType(JavaExec).configureEach {
  javaLauncher.set(javaToolchains.launcherFor(java.toolchain))
}

tasks.withType(Test).configureEach {
  useJUnitPlatform()
  failFast = true

  testLogging {
    exceptionFormat = 'full'
  }
}

tasks.register('functional', Test) {
  description = "Runs functional tests"
  group = "Verification"
  testClassesDirs = sourceSets.functionalTest.output.classesDirs
  classpath = sourceSets.functionalTest.runtimeClasspath
}

tasks.register('integration', Test) {
  description = "Runs integration tests"
  group = "Verification"
  testClassesDirs = sourceSets.integrationTest.output.classesDirs
  classpath = sourceSets.integrationTest.runtimeClasspath
  failFast = true
}

tasks.register('smoke', Test) {
  description = "Runs Smoke Tests"
  group = "Verification"
  testClassesDirs = sourceSets.smokeTest.output.classesDirs
  classpath = sourceSets.smokeTest.runtimeClasspath
}

jacocoTestReport {
  executionData(test, integration)
  reports {
    xml.getRequired().set(true)
    csv.getRequired().set(false)
    html.getRequired().set(true)
  }
}

tasks.sonarqube.dependsOn(jacocoTestReport)
tasks.check.dependsOn(integration)

sonarqube {
  properties {
    property "sonar.projectName", "HMCTS :: opal-logging-service"
    property "sonar.projectKey", "uk.gov.hmcts:opal-logging-service"
  }
}

// before committing a change, make sure task still works
dependencyUpdates {
  def isNonStable = { String version ->
    def stableKeyword = ['RELEASE', 'FINAL', 'GA'].any { qualifier -> version.toUpperCase().contains(qualifier) }
    def regex = /^[0-9,.v-]+$/
    return !stableKeyword && !(version ==~ regex)
  }
  rejectVersionIf { selection -> // <---- notice how the closure argument is named
    return isNonStable(selection.candidate.version) && !isNonStable(selection.currentVersion)
  }
}

// https://jeremylong.github.io/DependencyCheck/dependency-check-gradle/configuration.html
dependencyCheck {
  suppressionFile = 'config/owasp/suppressions.xml'
}

repositories {
  mavenLocal()
  mavenCentral()
  maven {
    url = uri('https://pkgs.dev.azure.com/hmcts/Artifacts/_packaging/hmcts-lib/maven/v1')
  }
}

ext {
  log4JVersion = "2.25.3"
  logbackVersion = "1.5.24"
}

ext['snakeyaml.version'] = '2.0'

dependencies {
  openapiImplementation "io.swagger.parser.v3:swagger-parser:2.1.37"
  openapiImplementation "io.swagger.core.v3:swagger-models:2.2.41"
  openapiImplementation "io.swagger.core.v3:swagger-core:2.2.41"
  openapiImplementation "com.fasterxml.jackson.dataformat:jackson-dataformat-yaml:2.20.1"

  implementation 'org.springframework.boot:spring-boot-starter-web'
  implementation 'org.springframework.boot:spring-boot-starter-actuator'
  implementation 'org.springframework.boot:spring-boot-starter-aop'
  implementation 'org.springframework.boot:spring-boot-starter-json'
  implementation 'org.springframework.boot:spring-boot-starter-data-jpa'
  implementation 'org.springdoc:springdoc-openapi-starter-webmvc-ui:2.8.15'

  implementation 'com.github.hmcts.java-logging:logging:7.0.0'

  implementation "org.apache.logging.log4j:log4j-api:${log4JVersion}"
  implementation "org.apache.logging.log4j:log4j-to-slf4j:${log4JVersion}"
  implementation "ch.qos.logback:logback-classic:${logbackVersion}"
  implementation "ch.qos.logback:logback-core:${logbackVersion}"
  implementation 'uk.gov.hmcts:spring-exception-handler:0.0.3'
  implementation 'org.flywaydb:flyway-core:11.20.0'
  runtimeOnly 'org.flywaydb:flyway-database-postgresql:11.20.0', {
    exclude group: 'org.flywaydb', module: 'flyway-core'
  }
  implementation 'org.postgresql:postgresql'
  compileOnly 'org.projectlombok:lombok:1.18.42'
  annotationProcessor 'org.projectlombok:lombok:1.18.42'
  implementation 'org.mapstruct:mapstruct:1.6.3'
  annotationProcessor 'org.mapstruct:mapstruct-processor:1.6.3'

  testImplementation 'org.springframework.boot:spring-boot-devtools'
  testImplementation 'org.springframework.boot:spring-boot-starter-test'
  testImplementation 'io.rest-assured:rest-assured:5.5.6'
  testImplementation 'org.springframework.boot:spring-boot-testcontainers'
  testImplementation 'org.testcontainers:junit-jupiter:1.21.4'
  testImplementation 'org.testcontainers:postgresql'

  //Added to override dependency version to resolve vulnerability
  implementation 'org.apache.commons:commons-lang3:3.20.0'
}

bootJar {
  archiveFileName = "opal-logging-service.jar"

  manifest {
    attributes('Implementation-Version': project.version.toString())
  }
}

// Gradle 7.x issue, workaround from: https://github.com/gradle/gradle/issues/17236#issuecomment-894768083
rootProject.tasks.named("processSmokeTestResources") {
  duplicatesStrategy = 'include'
}
rootProject.tasks.named("processFunctionalTestResources") {
  duplicatesStrategy = 'include'
}
rootProject.tasks.named("processIntegrationTestResources") {
  duplicatesStrategy = 'include'
}
flyway {
  url = System.getenv('FLYWAY_URL')
  user = System.getenv('FLYWAY_USER')
  password = System.getenv('FLYWAY_PASSWORD')
  locations = System.getenv('FLYWAY_LOCATIONS')
  baselineOnMigrate = true
  baselineVersion = '000'
}

wrapper {
  distributionType = Wrapper.DistributionType.ALL
}
checkstyle {
  // keep generated sources out of Checkstyle so only handwritten code is verified
  def generatedPatterns = [
    '**/build/generated/**',
    '**/uk/gov/hmcts/opal/generated/**',
    '**/uk/gov/hmcts/opal/logging/generated/**',
    '**/org/openapitools/**'
  ]
  checkstyleMain.exclude generatedPatterns
  checkstyleTest.exclude generatedPatterns
  checkstyleIntegrationTest.exclude generatedPatterns
  checkstyleFunctionalTest.exclude generatedPatterns
  checkstyleSmokeTest?.exclude generatedPatterns
}
tasks.withType(Checkstyle).configureEach {
  exclude '**/build/generated/**'
}
tasks.named("checkstyleMain").configure {
  def manualMainDirs = sourceSets.main.java.srcDirs.findAll { !it.toPath().startsWith(buildDir.toPath()) }
  setSource(files(manualMainDirs))
}
tasks.register('runAllStyleChecks') {
  dependsOn 'checkstyleMain'
  dependsOn 'checkstyleTest'
  dependsOn 'checkstyleIntegrationTest'
  dependsOn 'checkstyleFunctionalTest'
}

tasks.named("openApiGenerate") {
  dependsOn 'bundleOpenApi'
}
tasks.named("compileJava") {
  dependsOn 'openApiGenerate'
}
